#This file contains helper functions

define VowelUnitStartL [",@"|"@"] (["'"|"''"]);
define VowelUnitStartLNS [",@"|"@"];
define ConsUnitStartL [",#"|"#"]; #Consonant units can't be stressed
define SegBoundary [[" " | .#.] ([VowelUnitStartL | ConsUnitStartL])]; #Segments end on a space (or the end of input)
define SegBoundaryHard [" " | .#.]; #Used from phase 3.6 onwards, once everything has been grouped
define GroupBoundary [[VowelUnitStartL | ConsUnitStartL]| SegBoundary];
define UnitBoundary [GroupBoundary|"-"];
define UnitBoundaryHard [VowelUnitStartL | ConsUnitStartL | SegBoundaryHard];

define VowelAnt ["\pln"|"\nat"|"\brd"|"\clr"|"\ctr"|"\iot"|"\rnd"|"\opq"|"\idp"|"\udp"|"\natpos"];
define ConsonantAnt ["\co"|"\vo"|"\no"|"\svo"|"\sno"|"\hvo"|"\hno"|"\w"|"\y"|"\sch"]; #I think sch should be a consonant ant because it makes consonant sequences make sense e.g. "ism" -> "is-e-m"

define Ant ["\si"|"\st"|"\stst"|"\sel"|"\ser"|"\selr"|"\serl"|"\se"|VowelAnt|ConsonantAnt]; #All possible annotations after preprocessing

#Recall that we have transformed nested annotations for stresses to the following
#\brd{\st{a}} -> \brd${a}, \brd{\stst{a}} -> \brd$${a}

define StressMarker     ["$"|"$$"];
define WithAnt(AN, X)   [AN (StressMarker) "{" X "}"]; #only X with annotation AN applied
define EAnt(AN)         WithAnt(AN, 0); #Empty annotation AN
define AnyAnt(X)        [WithAnt(Ant, X) | X]; #X with any annotation applied
define AnyAntL(X)       [X ("}")]; #X possibly with an annotation on the left
define AnyAntR(X)       [([Ant (StressMarker) "{"]) X]; #X possibly with an annotation on the right
define NotInAntL        ["}" | GroupBoundary] [\Ant & \StressMarker]*; #on LHS of match, ensures that we're not matching within an annotation
define NotInAntR        [\"}"]* [Ant | StressMarker | GroupBoundary]; #on RHS of match, ensures that we're not matching within an annotation

#this guy is needed because WithAnt() on RHS of replace rule creates ambiguity
define WithAntR(AN, X)  [AN "{" X "}"];

define EarlyConsonant    [b|c|d|f|g|h|j|k|l|m|n|p|q|r|s|t|v|w|x|y|z];
define EarlyVowel        [a|e|i|o|u];

define Alphabet         [EarlyConsonant | EarlyVowel | Ant | "{" | "}" | "@" | "#" | "$" | "$$" | SegBoundary | GroupBoundary | "-" | "'" | "<-" | "<<-" | "->" | "->>"];

define LaterStressMarker ["'"|"''"|"_"];
